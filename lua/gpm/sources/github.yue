-- Seconds iteration of github source handler

_G = _G
import pairs, tostring, gpm from _G -- functions
import environment, Logger from gpm -- environment

import URL, Package, SourceError from environment -- classes
import async, await from environment -- functions

-- libraries
import Read, IsFile, IterateZipFiles, MountGMA, Set, Write from environment.file -- file.
import match, lower, sub, ByteSplit, IndexOf, StartsWith from environment.string -- string.
import getRepository, getTree, getBlob, fetchZip from environment.github -- github.
import CRC, JSONToTable, TableToJSON, ByteStream from environment.util -- util.
import getFile, getDirectory, join from environment.path -- path.
import GMA from environment.addon -- addon.

-- constants
CACHE_DIR = "/data/gpm/cache/github/"

-- url syntax:
-- github:user/repo[/branch]
-- or
-- github://user/repo[/branch]
class GithubSource extends gpm.loader.Source
    GetDefaultBranch = ( user, repository ) ->
        -- if we have local default branch, just use it and do not fetch it from github
        -- probably it wont be changed, and there is no need to recheck it every time
        filePath = CACHE_DIR .. user .. "/" .. repository .. "/default_branch.txt"

        branch = Read( filePath, nil, nil, nil, true )
        if branch
            return branch

        Logger\Debug( "Fetching information for Github repository '%s/%s'...", user, repository )

        branch = ( await getRepository( user, repository ) ).default_branch
        unless branch
            error SourceError "Failed to fetch default branch for '#{user}/#{repository}' from Github API."

        -- save the default branch to the cache
        Write( filePath, branch, nil, nil, true )

        return branch

    -- pattern: priority
    PACKAGE_PATH_PRIORITIES = {
        "^package%..+$": 10
        "package.yue": 11
        "package.moon": 12
        "package.lua": 15
        "package%..+": 20
    }

    FindPackageInfo = ( user, repository, tree_sha ) ->
        filePath = CACHE_DIR .. user .. "/" .. repository .. "/" .. tree_sha .. "/package.entry.json"
        if entry := JSONToTable( Read( filePath, nil, nil, nil, true ) or "", true, true )
            return entry

        Logger\Debug( "Fetching file tree from Github repository '%s/%s/%s'...", user, repository, tree_sha )

        res = await getTree( user, repository, tree_sha, true )

        entries = []
        for entry in *res.tree
            if entry.type == "blob" and match( entry.path, "package%..+$" )
                entries[] = entry

        packageEntry = nil

        if #entries == 1
            packageEntry = entries[1]

        else
            -- welp, we have multiple package.lua files, lets try to find the correct one
            priority = math.huge -- 0 is the highest priority
            for entry in *entries
                for pattern, p in pairs PACKAGE_PATH_PRIORITIES
                    if match( entry.path, pattern )
                        if pattern == entry.path
                            p = p - 10 -- paths that match the exact pattern are more important

                        if p < priority
                            priority = p
                            packageEntry = entry

                        -- TODO: check if we have duplicates (i.e packages/a/package.lua and packages/b/package.lua)

        if packageEntry
            Write( filePath, TableToJSON( packageEntry, false ), nil, nil, true )

        return packageEntry

    FetchPackageFile = ( user, repository, branch, entry ) ->
        filePath = CACHE_DIR .. user .. "/" .. repository .. "/" .. branch .. "/package.txt"
        if package := Read( filePath, nil, nil, nil, true )
            return package

        Logger\Debug( "Fetching package file from Github repository '%s/%s/%s'... (sha = '%s')", user, repository, branch, entry.sha )

        res = await getBlob( user, repository, entry.sha )
        Write( filePath, res.content, nil, nil, true )
        return res.content

    DownloadRepository = ( user, repository, branch ) ->
        filePath = CACHE_DIR .. user .. "/" .. repository .. "/" .. branch .. "/files.zip.dat"
        if data := Read( filePath, nil, nil, nil, true )
            return ByteStream( data )

        Logger\Debug( "Downloading repository '%s/%s/%s'...", user, repository, branch )
        data = await fetchZip( user, repository, branch )

        Write( filePath, data, nil, nil, true )

        return ByteStream( data )

    FetchInfo: async ( url ) =>
        -- Parse user, repo and branch from the given url
        segments = ByteSplit( url.pathname, 0x2F --[[ / ]] )
        if hostname := url.hostname
            insert( segments, 1, hostname )

        user = lower( segments[ 1 ] )
        repository = lower( segments[ 2 ] )
        unless user and user != "" and repository and repository != ""
            error SourceError "Invalid url '#{url}' (missing user or repository, got '#{user}' and '#{repository}')."

        branch = segments[ 3 ] or GetDefaultBranch( user, repository )

        packageEntry = FindPackageInfo( user, repository, branch )
        unless packageEntry
            error SourceError "Failed to find package file in #{user}/#{repository} (#{branch})."

        -- Check if repository already was installed locally
        if pkg := await Package.read( url )
            return {
                package: pkg
                url: url
                metadata: {
                    :user
                    :repository
                    :branch
                    :packageEntry
                    cached: true
                }
            }

        packageURL = URL( getFile( packageEntry.path ), @WorkingDirectory( url ) )

        packageContent = FetchPackageFile( user, repository, branch, packageEntry )
        unless packageContent
            error SourceError "Failed to fetch package file from #{url}."

        -- preventing overwriting existing package file
        unless IsFile( packageURL.pathname )
            Set( packageURL.pathname, packageContent )

        pkg = await Package.read( packageURL )
        unless pkg
            error SourceError "Failed to read package file from #{packageURL}. (url = #{url})"

        return {
            package: pkg
            url: url
            metadata: {
                :user
                :repository
                :branch
                :packageEntry
            }
        }

    mountedRepositories = {}

    Install: async ( info, workdir ) =>
        unless workdir
            workdir = @WorkingDirectory( info.url ).pathname

        if mountedRepositories[ workdir ]
            return nil

        root = getDirectory( info.metadata.packageEntry.path )
        rootLength = #root + 1 -- +1 to remove the trailing slash

        handle = DownloadRepository( info.metadata.user, info.metadata.repository, info.metadata.branch )

        -- just in case if Install was called multiple times
        if mountedRepositories[ workdir ]
            return nil

        Logger\Debug( "Installing package '%s@%s' from Github repository '%s/%s/%s'...", info.package.name, info.package.version, info.metadata.user, info.metadata.repository, info.metadata.branch )

        gmaPath = CACHE_DIR .. info.metadata.user .. "/" .. info.metadata.repository .. "/" .. info.metadata.branch .. "/files-" .. CRC( workdir ) .. ".gma"
        if Read( gmaPath, nil, nil, nil, true )
            unless MountGMA( gmaPath )
                error SourceError "Failed to mount GMA file '#{gmaPath}'."

            return nil

        gma = GMA!
        gma\SetTitle( info.url.href )

        for entry, err in IterateZipFiles( handle, false ) -- entry: { path, content }
            if err
                Logger\Debug( "Skipping file from zipball '%s/%s/%s' with path '%s' and reason '%s'", info.metadata.user, info.metadata.repository, info.metadata.branch, entry.path, err )
                continue

            -- first remove first directory from the path (appended by github)
            entryPath = sub( entry.path, IndexOf( entry.path, "/" ) + 1 )

            -- then remove the root directory
            unless StartsWith( entryPath, root )
                continue

            entryPath = sub( entryPath, rootLength ) -- TODO: check if string is even valid
            if entryPath == ""
                continue

            -- add working directory
            entryPath = join( workdir, entryPath )

            gma\SetFile( entryPath, entry.content )

        await gma\AsyncWrite( gmaPath, true, true )

        unless MountGMA( gmaPath )
            error SourceError "Failed to mount GMA file '#{gmaPath}'."

        mountedRepositories[ workdir ] = true
        return nil

GithubSource( "github" )
