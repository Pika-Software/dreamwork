import gpm, pairs from _G
import environment from gpm
import await, async, file, util, ModuleError, SourceError from environment

import AsyncImport, BaseSource, BaseSourceHandler, Module from gpm.loader
import AsyncWrite, MountZIPData from file
import CachedFetch from environment.http
import MD5ID from util

local getExtension
do

    import extname from environment.path

    luaExtensions = {
        "luac": true
        "lua": true
        "lc": true
    }

    getExtension = ( url ) ->
        extension = url.extension
        unless extension
            extension = extname( url.pathname ) or ""
            if luaExtensions[ extension ]
                extension = "lua"

            url.extension = extension

        return extension

local LuaCodeHandler
do

    import CompileLuaString, CompileMoonString, CompileYueString from util

    extensions = {
        "moon": true
        "yue": true
        "lua": true
    }

    class LuaCodeHandler extends BaseSourceHandler
        ShouldHandle: ( url ) =>
            return extensions[ getExtension( url ) ]

        FetchInfo: async ( url ) =>
            result = await CachedFetch( url.href )
            if result.status ~= 200
                error SourceError "failed to fetch " .. url.href

            return result.body

        Install: async ( url, content, _, env ) =>
            func = await switch url.extension
                when "yue"
                    yueConfig = {}
                    for key, value in url.searchParams\iterator!
                        yueConfig[ key ] = value

                    CompileYueString( content, url.href, true, yueConfig )

                when "moon"
                    CompileMoonString( content, url.href, true )

                else
                    CompileLuaString( content, url.href, true, false )

            unless func
                error ModuleError "File '" .. url.href .. "' cannot be compiled."
                return

            return Module.run( func, url, env )

class TextHandler extends BaseSourceHandler
    ShouldHandle: ( url ) =>
        return getExtension( url ) == "txt"

    FetchInfo: async ( url ) =>
        return url.href

    Install: async ( url, href ) =>
        result = await CachedFetch( href )
        if result.status ~= 200
            error SourceError "failed to fetch " .. href

        return result.body

local JSONHandler
do

    import JSONToTable from util

    class JSONHandler extends BaseSourceHandler
        ShouldHandle: ( url ) =>
            return getExtension( url ) == "json"

        FetchInfo: async ( url ) =>
            result = await CachedFetch( url.href )
            if result.status ~= 200
                error SourceError "failed to fetch " .. url.href

            return result.body

        Install: async ( url, json ) =>
            tbl = JSONToTable( json, true, false )
            if tbl
                return tbl

            error SourceError "failed to parse json"
            return

class GMAHandler extends BaseSourceHandler
    ShouldHandle: ( url ) =>
        return getExtension( url ) == "gma"

    FetchInfo: async ( url ) =>
        result = await CachedFetch( url.href )
        if result.status ~= 200
            error SourceError "failed to fetch " .. url.href

        return AsyncWrite( "gpm/mount/" .. MD5ID( url.href ) .. ".gma", result.body )

    Install: async ( url, data ) =>
        return AsyncImport( "file://data/" .. data.fileName )

class ZIPHandler extends BaseSourceHandler
    ShouldHandle: ( url ) =>
        return getExtension( url ) == "zip"

    FetchInfo: async ( url ) =>
        result = await CachedFetch( url.href )
        if result.status ~= 200
            error SourceError "failed to fetch " .. url.href

        return result.body

    Install: async ( url, data ) =>
        return MountZIPData( data, url.href )

class WebSource extends BaseSource

ws = WebSource( "http", "https" )
ws\RegisterHandler( LuaCodeHandler! )
ws\RegisterHandler( TextHandler! )
ws\RegisterHandler( JSONHandler! )
ws\RegisterHandler( GMAHandler! )
ws\RegisterHandler( ZIPHandler! )
