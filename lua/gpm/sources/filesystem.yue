_G = _G
import gpm from _G
import environment from gpm
import file, async, await, ModuleError, MountError, SourceError, error from environment
import BaseSource, BaseSourceHandler, Module from gpm.loader
import getExtension, equal from environment.path
import STATE_PENDING from environment.Promise
import NormalizeGamePath from file

do

    import IsBinaryModuleInstalled from environment.util
    import require from _G

    class BinarySourceHandler extends BaseSourceHandler
        Install: async ( url ) =>
            name = url.pathname
            if not name or name == ""
                error SourceError "Binary module name is not specified!"
                return

            unless IsBinaryModuleInstalled( name )
                error SourceError "Binary module " .. name .. " is not installed locally!"
                return

            require( name )
            return _G[ name ]

    class BinarySource extends BaseSource

    BinarySource( "dll" )\RegisterHandler( BinarySourceHandler! )

do

    import File from environment.addon

    class GMAHandler extends BaseSourceHandler
        ShouldHandle: ( url ) =>
            return getExtension( url.pathname ) == "gma"

        Install: async ( url ) =>
            f = File!
            await f\AsyncRead( url.pathname, true, true )
            -- f\SetTitle( url.href )
            unless f\VerifyCRC!
                error SourceError "Invalid CRC checksum for '" .. url.href .. "'"

            return f\AsyncMount( false )

    local ZIPHandler
    do

        import MountZIP from file

        promises = {}

        mountZip = ( filePath ) ->
            promise = promises[ filePath ]
            if promise and promise.state == STATE_PENDING
                return promise

            filePath, gamePath = NormalizeGamePath( filePath, gamePath )
            promise = MountZIP( filePath, gamePath, true )
            promises[ filePath ] = promise
            return promise

        class ZIPHandler extends BaseSourceHandler
            ShouldHandle: ( url ) =>
                return getExtension( url.pathname ) == "zip"

            Install: async ( url ) =>
                return await mountZip( url.pathname )

    local LuaHandler
    do

        import sub from _G.string
        import Compile from file

        promises = {}

        compile = ( filePath, handleError, yueConfig ) ->
            promise = promises[ filePath ]
            if promise and promise.state == STATE_PENDING
                return promise

            filePath, gamePath = NormalizeGamePath( filePath, gamePath )
            promise = Compile( filePath, gamePath, handleError, yueConfig, true )
            promises[ filePath ] = promise
            return promise

        class LuaHandler extends BaseSourceHandler
            ShouldHandle: ( url ) =>
                return equal( sub( url.pathname, 1, 5 ), "/lua/" )

            FetchInfo: ( url ) =>
                yueConfig = {}
                for key, value in url.searchParams\iterator!
                    yueConfig[ key ] = value

                return await compile( url.pathname, true, yueConfig )

            Install: async ( url, func, _, env ) =>
                if func
                    return Module.run( func, url, env )

                error ModuleError "File '" .. url.pathname .. "' cannot be compiled."
                return

    class FileSource extends BaseSource

    fs = FileSource( "file" )
    fs\RegisterHandler( LuaHandler! )
    fs\RegisterHandler( ZIPHandler! )
    fs\RegisterHandler( GMAHandler! )
