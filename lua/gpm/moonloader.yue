gpm = gpm
:util, :Logger = gpm

if util.IsBinaryModuleInstalled( "moonloader" )
    local ok, message = pcall( require, "moonloader" )
    if ok
        Logger\Info( "gm_moonloader v%s initialized.", util.Version( moonloader._VERSION ) )
    else
        Logger\Error( "gm_moonloader startup error: %s", message )
        return
else
    Logger\Warn( "gm_moonloder is missing, support of yuescript and moonscript is disabled, running packages using these languages is not possible!" )
    return

CompileString = CompileString
isfunction = isfunction
istable = istable
:ArgAssert = gpm
error = error

-- MoonScript
moonloader = moonloader
if istable( moonloader )
    gpm.moonloader = moonloader
    moonloader_ToLua = moonloader.ToLua

    util.CompileMoonString = ( moonCode, identifier, handleError ) ->
        ArgAssert( moonCode, "string", 1 )
        ArgAssert( identifier, "string", 2 )

        luaCode, message = moonloader_ToLua( moonCode )
        message = message or "MoonScript to Lua compilation failed."

        unless luaCode
            error( message )

        func = CompileString( luaCode, identifier, handleError == true )
        unless isfunction( func )
            error( message )

        return func

    Logger\Info( "MoonScript: Found" )

else

    Logger\Info( "MoonScript: Missing" )

-- Yuescript
yue = moonloader and moonloader.yue
if istable( yue )
    yue_ToLua = yue.ToLua

    -- https://github.com/pigpigyyy/Yuescript/src/yuescript/yue_compiler.h#L28
    util.CompileYueString = ( yueCode, identifier, handleError, yueConfig ) ->
        ArgAssert( yueCode, "string", 1 )
        ArgAssert( identifier, "string", 2 )
        if yueConfig ~= nil
            ArgAssert( yueConfig, "table", 4 )

        luaCode, message = yue_ToLua( yueCode, yueConfig )
        message = message or "Yuescript to Lua compilation failed."

        unless luaCode
            error( message )

        func = CompileString( luaCode, identifier, handleError == true )
        unless isfunction( func )
            error( message )

        return func

    Logger\Info( "Yuescript: Found" )

else

    Logger\Info( "Yuescript: Missing" )
