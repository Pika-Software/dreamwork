import environment, Logger from gpm
import util from environment

if util.IsBinaryModuleInstalled( "moonloader" )
    local ok, msg = pcall( require, "moonloader" )
    if ok and istable( moonloader )
        Logger\Loaded( "gm_moonloader v%s", util.Version( moonloader._VERSION ) )
    else
        Logger\Error( "gm_moonloader startup error: %s", msg or "unknown error" )
        return

unless istable( moonloader )
    Logger\Warn( "gm_moonloader is missing, support for moon and yue scripts is unavailable!" )
    return

environment.moonloader = moonloader

import isfunction, istable, error from _G
import CompileLuaString from util
import ArgAssert from environment

-- MoonScript
do

    import ToLua from moonloader

    util.CompileMoonString = ( moonCode, identifier, handleError ) ->
        ArgAssert( moonCode, 1, "string" )
        ArgAssert( identifier, 2, "string" )

        luaCode, msg = ToLua( moonCode )
        unless luaCode
            error( msg or "MoonScript compilation failed." )

        func = CompileLuaString( luaCode, identifier, handleError == true )
        unless isfunction( func )
            error( msg or "MoonScript compilation failed." )

        return func

-- Yuescript
:yue = moonloader

unless istable( yue )
    Logger\Warn( "Yuescript support is missing, yue package execution is unavailable!" )
    return

import ToLua from yue
emptyTable = {}

-- https://github.com/pigpigyyy/Yuescript/src/yuescript/yue_compiler.h#L28
util.CompileYueString = ( yueCode, identifier, handleError, yueConfig ) ->
    ArgAssert( yueCode, 1, "string" )
    ArgAssert( identifier, 2, "string" )

    if yueConfig ~= nil
        ArgAssert( yueConfig, 4, "table" )
    else
        yueConfig = emptyTable

    luaCode, msg = ToLua( yueCode, yueConfig )
    unless luaCode
        error( msg or "Yuescript compilation failed." )

    func = CompileLuaString( luaCode, identifier, handleError == true )
    unless isfunction( func )
        error( msg or "Yuescript compilation failed." )

    return func
